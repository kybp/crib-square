(require-extension format)
(load "crib-square.ss")

(define (print-explanation combination)
  (let ((type (combination-type combination)))
    (format #t "  ~a points - ~a - " (combination-value combination) type)
    (if (member type '(little-flush big-flush))
        (display (combination-flush-suit combination))
        (format #t "~{~a~^ ~}"
                (map card->string (combination-cards combination))))
    (newline)))

(define (finish-game tableau deck)
  (let ((starter (car (draw-card deck))))
    (format #t "You got ~a points.~%" (game-value tableau starter))
    (format #t "Explain? (y/n) ")
    (unless (member (read) '(n no q exit))
            (for-each (lambda (hand)
                        (format #t "Hand: ~{~a~^ ~} (~a) - ~a points~%"
                                (map card->string hand)
                                (card->string starter)
                                (hand-value hand starter))
                        (when (jack-point? hand starter)
                              (format #t "  jack - 1 point~%"))
                        (for-each print-explanation (score-hand hand starter)))
                      (tableau-hands tableau)))))

(let loop ((tableau (make-tableau))
           (deck (shuffle (make-deck)))
           (turns 0))
  (print-tableau tableau)
  (if (= turns 16) (finish-game tableau deck)
      (let* ((again (lambda () (loop tableau deck turns)))
             (result (draw-card deck))
             (card (car result))
             (deck (cdr result)))
        (format #t "You drew ~a. Enter a column and row number to place it.~%"
                (card->string card))
        (let* ((column (read))
               (row (read)))
          (cond ((not (<= 0 row 3))
                 (format #t "Invalid move: row must be between 0 and 3.~%")
                 (again))
                ((not (<= 0 column 3))
                 (format #t "Invalid move: column must be between 0 and 3.~%")
                 (again))
                ((get-card tableau row column)
                 (format #t "That space is already taken.~%")
                 (again))
                (else (loop (play-card card row column tableau)
                            deck
                            (+ turns 1))))))))
